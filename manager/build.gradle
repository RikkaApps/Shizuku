import java.nio.file.Paths

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "moe.shizuku.privileged.api"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs 'en', 'ar', 'cs', 'de', 'es-rCL', 'b+es+419', 'eo', 'hu', 'hy', 'ja', 'ko', 'pl', 'pt-rBR', 'ru', 'tr', 'uk', 'zh-rCN', 'zh-rTW'
        resValue "string", "versionName", "${rootProject.ext.versionName}"
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=none'
            }
        }
    }
    buildFeatures {
        viewBinding true
        prefab true
    }
    signingConfigs {
        sign
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.sign
        }
        release {
            signingConfig signingConfigs.sign
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true
    }
    packagingOptions {
        exclude '/META-INF/*.version'
        exclude '/META-INF/*.kotlin_module'
        exclude '/kotlin/**'
    }
    lintOptions {
        checkReleaseBuilds false
    }
    dependenciesInfo {
        includeInApk false
    }
}

def optimizeReleaseResources = task('optimizeReleaseResources').doLast {
    def aapt2 = Paths.get(project.android.sdkDirectory.path, 'build-tools', project.android.buildToolsVersion, 'aapt2')
    def zip = Paths.get(project.buildDir.path, 'intermediates',
            'processed_res', 'release', 'out', "resources-release.ap_")
    def optimized = new File("${zip}.opt")
    def cmd = exec {
        commandLine aapt2, 'optimize', '--collapse-resource-names',
                '--shorten-resource-paths',
                '--resources-config-path', 'aapt2-resources.cfg',
                '-o', optimized, zip
        ignoreExitValue false
    }
    if (cmd.exitValue == 0) {
        delete(zip)
        optimized.renameTo("$zip")
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'processReleaseResources') {
        task.finalizedBy optimizeReleaseResources
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        def variantCapped = variant.name.capitalize()

        outputFileName = "shizuku-v${variant.versionName}-${variant.name}.apk"

        variant.assembleProvider.get().dependsOn(":shell:assemble$variantCapped")
        variant.assembleProvider.get().doLast {
            def outDir = new File(rootDir, "out")
            def mappingDir = new File(outDir, "mapping").absolutePath
            def apkDir = new File(outDir, "apk").absolutePath

            if (variant.getBuildType().isMinifyEnabled()) {
                copy {
                    from variant.mappingFileProvider.get()
                    into mappingDir
                    rename { String fileName -> "mapping-${variant.versionName}.txt" }
                }
                copy {
                    from outputFile
                    into apkDir
                }
            }
        }
    }
}

configurations.all {
    exclude group: 'androidx.appcompat', module: 'appcompat'
}

repositories {
    maven {
        url 'https://jitpack.io'
        content {
            includeGroup('com.github.topjohnwu.libsu')
        }
    }
    mavenLocal()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1"

    //compileOnly project(':hidden-api-common')
    implementation project(':hidden-api-common-bridge')
    implementation project(':server')
    implementation project(':bsh')
    implementation project(':starter')
    implementation project(':api')
    implementation project(':provider')

    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.5'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'

    implementation 'com.github.topjohnwu.libsu:core:3.1.2'

    implementation 'dev.rikka.rikkax.appcompat:appcompat:1.2.0-rc01'
    implementation 'dev.rikka.rikkax.core:core:1.3.2'
    implementation 'dev.rikka.rikkax.material:material:1.6.5'
    implementation 'dev.rikka.rikkax.html:html-ktx:1.1.2'
    implementation 'dev.rikka.rikkax.recyclerview:recyclerview-adapter:1.3.0'
    implementation 'dev.rikka.rikkax.recyclerview:recyclerview-ktx:1.2.2'
    implementation 'dev.rikka.rikkax.insets:insets:1.1.0'
    implementation 'dev.rikka.rikkax.widget:borderview:1.0.1'
    implementation 'dev.rikka.rikkax.preference:simplemenu-preference:1.0.3'

    implementation 'io.github.vvb2060.ndk:boringssl:2.1'
    implementation 'dev.rikka.ndk.thirdparty:cxx:1.1.0'

    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:2.0'

    implementation 'org.bouncycastle:bcpkix-jdk15on:1.68'

    implementation 'me.zhanghai.android.appiconloader:appiconloader:1.3.1'
}

apply from: rootProject.file('signing.gradle')
