import java.nio.file.Paths

plugins {
    id('com.android.application')
    id('kotlin-android')
    id('dev.rikka.tools.refine')
    id('dev.rikka.tools.autoresconfig')
    id('dev.rikka.tools.materialthemebuilder')
}

android {
    namespace 'moe.shizuku.manager'
    defaultConfig {
        applicationId "moe.shizuku.privileged.api"
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=none'
            }
        }
    }
    buildFeatures {
        viewBinding true
        prefab true
    }
    signingConfigs {
        sign
    }
    buildTypes {
        debug {
            defaultConfig.minSdk 24
            signingConfig signingConfigs.sign
        }
        release {
            signingConfig signingConfigs.sign
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    packagingOptions {
        jniLibs {
            excludes += ['/kotlin/**']
            useLegacyPackaging false
        }
        resources {
            excludes += ['/META-INF/*.version', '/META-INF/*.kotlin_module', '/kotlin/**']
        }
    }
    dependenciesInfo {
        includeInApk false
    }
    lint {
        checkReleaseBuilds false
    }
}

autoResConfig {
    generatedClassFullName = "rikka.shizuku.manager.ShizukuLocales"
    generateRes = false
    generatedArrayFirstItem = "SYSTEM"
    generateLocaleConfig = true
}

materialThemeBuilder {
    themes {
        shizuku {
            primaryColor = "#3F51B5"
            lightThemeFormat = "Theme.Material3.Light.%s"
            lightThemeParent = "Theme.Material3.Light.Rikka"
            darkThemeFormat = "Theme.Material3.Dark.%s"
            darkThemeParent = "Theme.Material3.Dark.Rikka"
        }
    }
    generatePalette = true
    generateTextColors = true
}

def collapseReleaseResourceNames = task('collapseReleaseResourceNames').doLast {
    def aapt2 = Paths.get(project.android.sdkDirectory.path, 'build-tools', project.android.buildToolsVersion, 'aapt2')
    def zip = Paths.get(project.buildDir.path, 'intermediates',
            'optimized_processed_res', 'release', 'resources-release-optimize.ap_')
    def optimized = new File("${zip}.opt")
    def cmd = exec {
        commandLine aapt2, 'optimize', '--collapse-resource-names',
                '--resources-config-path', 'aapt2-resources.cfg',
                '-o', optimized, zip
        ignoreExitValue false
    }
    if (cmd.exitValue == 0) {
        delete(zip)
        optimized.renameTo("$zip")
    }
}

afterEvaluate {
    tasks.getByName('optimizeReleaseResources').finalizedBy(collapseReleaseResourceNames)
    tasks.getByName('preReleaseBuild').dependsOn(':shell:assembleRelease')
    tasks.getByName('preDebugBuild').dependsOn(':shell:assembleDebug')
}

android.applicationVariants.configureEach { variant ->
    variant.outputs.configureEach {
        outputFileName = "shizuku-v${variant.versionName}-${variant.name}.apk"

        variant.assembleProvider.get().doLast {
            def outDir = new File(rootDir, "out")
            def mappingDir = new File(outDir, "mapping").absolutePath
            def apkDir = new File(outDir, "apk").absolutePath

            if (variant.getBuildType().isMinifyEnabled()) {
                copy {
                    from variant.mappingFileProvider.get()
                    into mappingDir
                    rename { String fileName -> "mapping-${variant.versionName}.txt" }
                }
                copy {
                    from outputFile
                    into apkDir
                }
            }
        }
    }
}

configurations.configureEach {
    exclude group: 'androidx.appcompat', module: 'appcompat'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    implementation project(':server')
    implementation project(':rish')
    implementation project(':starter')
    implementation project(':api')
    implementation project(':provider')

    annotationProcessor libs.refine.annotation.processor
    implementation libs.refine.annotation

    implementation libs.hidden.compat
    compileOnly libs.hidden.stub

    implementation 'androidx.browser:browser:1.5.0'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.fragment:fragment-ktx:1.5.5'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'

    implementation 'com.google.android.material:material:1.7.0'

    implementation 'com.github.topjohnwu.libsu:core:3.2.1'

    implementation 'dev.rikka.rikkax.appcompat:appcompat:1.6.1'
    implementation 'dev.rikka.rikkax.compatibility:compatibility:2.0.0'
    implementation 'dev.rikka.rikkax.core:core-ktx:1.4.1'
    implementation 'dev.rikka.rikkax.material:material:2.7.0'
    implementation 'dev.rikka.rikkax.material:material-preference:2.0.0'
    implementation 'dev.rikka.rikkax.html:html-ktx:1.1.2'
    implementation 'dev.rikka.rikkax.recyclerview:recyclerview-adapter:1.3.0'
    implementation 'dev.rikka.rikkax.recyclerview:recyclerview-ktx:1.3.2'
    implementation 'dev.rikka.rikkax.insets:insets:1.3.0'
    implementation 'dev.rikka.rikkax.layoutinflater:layoutinflater:1.3.0'
    implementation 'dev.rikka.rikkax.widget:borderview:1.1.0'
    implementation 'dev.rikka.rikkax.preference:simplemenu-preference:1.0.3'
    implementation 'dev.rikka.rikkax.lifecycle:lifecycle-resource-livedata:1.0.1'
    implementation 'dev.rikka.rikkax.lifecycle:lifecycle-shared-viewmodel:1.0.1'
    implementation 'dev.rikka.rikkax.lifecycle:lifecycle-viewmodel-lazy:2.0.0'

    implementation 'io.github.vvb2060.ndk:boringssl:2.1'
    implementation 'dev.rikka.ndk.thirdparty:cxx:1.2.0'

    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'

    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'

    implementation 'me.zhanghai.android.appiconloader:appiconloader:1.5.0'
}

apply from: rootProject.file('signing.gradle')
