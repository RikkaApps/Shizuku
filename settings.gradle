pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
        mavenLocal()
        maven {
            url("https://s01.oss.sonatype.org/content/repositories/releases/")
            content {
                includeGroup("dev.rikka.tools.refine")
                includeGroup("dev.rikka.tools.autoresconfig")
                includeGroup("dev.rikka.tools.materialthemebuilder")
            }
        }
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
        mavenLocal()
        maven { url 'https://jitpack.io' }
        maven {
            url("https://s01.oss.sonatype.org/content/repositories/releases/")
            content {
                includeGroupByRegex("dev.rikka.*")
                includeGroup("org.lsposed.hiddenapibypass")
            }
        }
    }
    versionCatalogs {
        libs {
            version('hidden-api', '4.3.3')
            library('hidden-compat', 'dev.rikka.hidden', 'compat').versionRef('hidden-api')
            library('hidden-stub', 'dev.rikka.hidden', 'stub').versionRef('hidden-api')

            version('refine', '4.3.0')
            library('refine-runtime', 'dev.rikka.tools.refine', 'runtime').versionRef('refine')
            library('refine-annotation', 'dev.rikka.tools.refine', 'annotation').versionRef('refine')
            library('refine-annotation-processor', 'dev.rikka.tools.refine', 'annotation-processor').versionRef('refine')
            plugin('refine', 'dev.rikka.tools.refine').versionRef('refine')
        }
    }
}

include ':server', ':starter', ':shell'
include ':manager'
include ':common'

def root = "api"

def propFile = file('local.properties')
def props = new Properties()

if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null) {
        if (props["api.useLocal"].equals("true")) {
            root = props["api.dir"]
        }
    }
}

include ':aidl'
project(':aidl').projectDir = file("$root${File.separator}aidl")

include ':rish'
project(':rish').projectDir = file("$root${File.separator}rish")

include ':shared'
project(':shared').projectDir = file("$root${File.separator}shared")

include ':api'
project(':api').projectDir = file("$root${File.separator}api")

include ':provider'
project(':provider').projectDir = file("$root${File.separator}provider")

include ':server-shared'
project(':server-shared').projectDir = file("$root${File.separator}server-shared")

include ':hidden-api-stub'
project(':hidden-api-stub').projectDir = file("$root${File.separator}hidden-api-stub")
