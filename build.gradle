buildscript {
    apply from: 'api/manifest.gradle'
    ext.kotlinVersion = '1.8.0'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'com.android.application' version '8.0.2' apply false
    id 'com.android.library' version '8.0.2' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.8.0' apply false
    id 'dev.rikka.tools.autoresconfig' version '1.2.1' apply false
    id 'dev.rikka.tools.materialthemebuilder' version '1.3.3' apply false
    alias libs.plugins.refine apply false
}

apply plugin: 'idea'

idea.module {
    excludeDirs += file('out')
}

allprojects {
    tasks.withType(Javadoc).tap {
        configureEach {
            failOnError false
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

subprojects {
    plugins.withId("com.android.base") {
        plugins.apply('dev.rikka.tools.refine')

        android {
            compileSdk = 33
            defaultConfig {
                minSdk = 23
                targetSdk = 33
            }
            compileOptions {
                sourceCompatibility = JavaVersion.VERSION_17
                targetCompatibility = JavaVersion.VERSION_17
            }
            buildFeatures {
                aidl true
            }
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

def gitCommitId = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
def baseVersionName = "${api_version_major}.5.4"
ext {
    minSdkVersion = 23
    targetSdkVersion = 33
    versionCode = gitCommitCount
    versionName = "${baseVersionName}.r${gitCommitCount}.${gitCommitId}"
}
