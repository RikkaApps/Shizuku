apply plugin: 'idea'

idea.module {
    excludeDirs += file('out')
}

buildscript {
    apply from: 'api/manifest.gradle'

    ext.kotlinVersion = '1.6.10'
    repositories {
        mavenCentral()
        google()

        // In case newly published library not yet synced with maven central
        maven {
            url("https://s01.oss.sonatype.org/content/repositories/releases/")
            content {
                includeGroup("dev.rikka.tools.refine")
                includeGroup("dev.rikka.tools.autoresconfig")
                includeGroup("dev.rikka.tools.materialthemebuilder")
            }
        }
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.18.0'
        classpath 'dev.rikka.tools.refine:gradle-plugin:3.0.3'
        classpath 'dev.rikka.tools.autoresconfig:gradle-plugin:1.0.0'
        classpath 'dev.rikka.tools.materialthemebuilder:dev.rikka.tools.materialthemebuilder.gradle.plugin:1.1.0'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()

        // In case newly published library not yet synced with maven central
        maven {
            url("https://s01.oss.sonatype.org/content/repositories/releases/")
            content {
                includeGroupByRegex("dev.rikka.*")
                includeGroup("org.lsposed.hiddenapibypass")
            }
        }
        mavenLocal()
    }
    tasks.withType(Javadoc) {
        failOnError false
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def gitCommitId = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
def baseVersionName = "${api_version_major}.6.1"
ext {
    minSdkVersion = 23
    targetSdkVersion = 31
    versionCode = gitCommitCount
    versionName = "${baseVersionName}.r${gitCommitCount}.${gitCommitId}"
}
